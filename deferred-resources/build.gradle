apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.plugin.parcelize'
apply plugin: 'dev.drewhamilton.extracare'
apply plugin: 'androidx.navigation.safeargs.kotlin'

ext {
    artifactName = 'deferred-resources'
    publishedDescription = 'Decoupling resource declaration from resource resolution.'
}
apply from: '../publish.gradle'

android {
    compileSdkVersion rootProject.ext.targetSdk
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionName version

        consumerProguardFiles 'consumer-rules.pro'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs += ['-Xexplicit-api=strict']
    }
}

dependencies {
    implementation deps.androidAnnotations

    implementation(deps.appCompat) {
        exclude group: 'androidx.fragment'
        exclude group: 'androidx.lifecycle'
    }
    implementation(deps.coreKtx) {
        exclude group: 'androidx.lifecycle'
    }

    // TODO WORKAROUND: Should be androidTestImplementation but SafeArgs doesn't work in androidTest/
    debugImplementation deps.coroutines
    debugImplementation deps.navigationFragment
    debugImplementation 'androidx.fragment:fragment:1.2.5'

    androidTestImplementation deps.junitAndroidX
    androidTestImplementation deps.androidTestRules
    androidTestImplementation deps.androidTestRunner
    androidTestImplementation deps.truth
    androidTestImplementation deps.navigationUi
    androidTestImplementation 'androidx.lifecycle:lifecycle-common:2.2.0'
    androidTestImplementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'
}
