buildscript {
    //region Dependency declarations
    ext.versions = [
        jaCoCo: '0.8.4',
        kotlin: '1.4.10',
    ]

    ext.deps = [
        androidAnnotations: 'androidx.annotation:annotation:1.1.0',
        androidTestRules: 'androidx.test:rules:1.3.0',
        androidTestRunner: 'androidx.test:runner:1.3.0',
        appCompat: 'androidx.appcompat:appcompat:1.2.0',
        coreKtx: 'androidx.core:core-ktx:1.3.2',
        coroutines: 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.6',
        junit: 'junit:junit:4.13',
        junitAndroidX: 'androidx.test.ext:junit-ktx:1.1.2',
        truth: 'com.google.truth:truth:1.0.1',
    ]
    //endregion

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0'

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.4.10'
        classpath 'org.jetbrains.kotlinx:binary-compatibility-validator:0.2.3'

        classpath 'dev.drewhamilton.extracare:extracare-gradle-plugin:0.3.1'
    }

    repositories {
        mavenCentral()
        google()
        jcenter()
    }
}

ext {
    publishGroup = 'com.backbase.oss.deferredresources'
    libraryVersion = '1.2.0-SNAPSHOT'
    minSdk = 14
    targetSdk = 30
    buildToolsVersion = '30.0.2'
}

apply plugin: 'binary-compatibility-validator'
apiValidation {
    ignoredProjects += ['demo']
}

allprojects { project ->
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    project.plugins.withType(com.android.build.gradle.LibraryPlugin) {
        //region Disable BuildConfig for libraries
        project.android.libraryVariants.all { variant ->
            // TODO WORKAROUND: Replace with https://issuetracker.google.com/issues/72050365 once released.
            variant.generateBuildConfigProvider.get().enabled = false
        }
        //endregion
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
