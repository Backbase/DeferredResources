/*
 * Copyright 2020 Backbase R&D B.V.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    dependencies {
        classpath(libs.android.gradlePlugin)

        classpath(libs.kotlin.gradlePlugin)
        classpath(libs.dokka.gradlePlugin)
        classpath(libs.kotlinx.binaryCompatibilityValidator)

        classpath(libs.poko.gradlePlugin)

        classpath(libs.detekt.gradlePlugin)
    }

    repositories {
        mavenCentral()
        google()
    }

    // TODO WORKAROUND: Remove when AGP no longer requires JCenter's trove4j
    configurations.configureEach {
        resolutionStrategy.eachDependency {
            if (requested.group == 'org.jetbrains.trove4j' &&
                requested.name == 'trove4j' &&
                requested.version == '20160824'
            ) {
                useTarget('org.jetbrains.intellij.deps:trove4j:1.0.20181211')
            }
        }
    }
}

ext {
    publishGroup = 'com.backbase.oss.deferredresources'
    libraryVersion = '1.4.0-SNAPSHOT'
    minSdk = 14
    targetSdk = 30
    buildToolsVersion = '30.0.3'
}

apply plugin: 'binary-compatibility-validator'
apiValidation {
    ignoredProjects += ['demo']
}

allprojects { project ->
    repositories {
        mavenCentral()
        google()

        // Transitive kotlinx-html-jvm dependency for Dokka:
        maven { url = "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven" }
    }

    project.plugins.withType(com.android.build.gradle.LibraryPlugin) {
        //region Disable BuildConfig for libraries
        project.android.libraryVariants.all { variant ->
            variant.generateBuildConfigProvider.get().enabled = false
        }
        //endregion
    }

    // TODO WORKAROUND: Remove when AGP no longer requires JCenter's trove4j
    configurations.configureEach {
        resolutionStrategy.eachDependency {
            if (requested.group == 'org.jetbrains.trove4j' &&
                requested.name == 'trove4j' &&
                requested.version == '20160824'
            ) {
                useTarget('org.jetbrains.intellij.deps:trove4j:1.0.20181211')
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
