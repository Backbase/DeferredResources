buildscript {
    dependencies {
        classpath(libs.android.gradlePlugin)

        classpath(libs.kotlin.gradlePlugin)
        classpath(libs.dokka.gradlePlugin)
        classpath(libs.kotlinx.binaryCompatibilityValidator)

        classpath(libs.poko.gradlePlugin)

        classpath(libs.detekt.gradlePlugin)
    }

    repositories {
        mavenCentral()
        google()

        // TODO: Remove Bintray dependencies when possible
        maven { url 'https://dl.bintray.com/kotlin/dokka' }
        maven { url 'https://dl.bintray.com/jetbrains/markdown' }
    }

    // TODO WORKAROUND: Remove when AGP no longer requires JCenter's trove4j
    configurations.configureEach {
        resolutionStrategy.eachDependency {
            if (requested.group == 'org.jetbrains.trove4j' &&
                requested.name == 'trove4j' &&
                requested.version == '20160824'
            ) {
                useTarget('org.jetbrains.intellij.deps:trove4j:1.0.20181211')
            }
        }
    }
}

ext {
    publishGroup = 'com.backbase.oss.deferredresources'
    libraryVersion = '1.4.0-SNAPSHOT'
    minSdk = 14
    targetSdk = 30
    buildToolsVersion = '30.0.3'
}

apply plugin: 'binary-compatibility-validator'
apiValidation {
    ignoredProjects += ['demo']
}

allprojects { project ->
    repositories {
        mavenCentral()
        google()

        // TODO: Remove Bintray dependencies when possible
        maven { url 'https://dl.bintray.com/jetbrains/markdown' }
        maven { url 'https://dl.bintray.com/korlibs/korlibs' }
        maven { url 'https://dl.bintray.com/kotlin/dokka' }
        maven { url 'https://dl.bintray.com/kotlin/kotlinx' }
    }

    project.plugins.withType(com.android.build.gradle.LibraryPlugin) {
        //region Disable BuildConfig for libraries
        project.android.libraryVariants.all { variant ->
            variant.generateBuildConfigProvider.get().enabled = false
        }
        //endregion
    }

    // TODO WORKAROUND: Remove when AGP no longer requires JCenter's trove4j
    configurations.configureEach {
        resolutionStrategy.eachDependency {
            if (requested.group == 'org.jetbrains.trove4j' &&
                requested.name == 'trove4j' &&
                requested.version == '20160824'
            ) {
                useTarget('org.jetbrains.intellij.deps:trove4j:1.0.20181211')
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
